
plugins {
  id "com.google.protobuf" version "0.8.13"
  id "java"
}

project.ext.set('protobufVersion', '3.12.0')
project.ext.set('protocVersion', project.protobufVersion)

repositories {
  jcenter()
  mavenCentral()
}

sourceSets {
  main {
    proto {
      srcDir file('./proto')
    }
  }
}

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')

protobuf {
  plugins {
    grpc {
      path = file(
        './node_modules/.bin/grpc_tools_node_protoc_plugin' + (isWindows ? '.cmd' : '')
      )
    }
    ts {
      path = file(
        './node_modules/.bin/protoc-gen-ts' + (isWindows ? '.cmd' : '')
      )
    }
  }
  generateProtoTasks {
    generateProto.finalizedBy syncGeneratedProto
    all().each { task ->
      task.plugins {
        grpc {
          outputSubDir = 'js'
          option 'grpc_js'
        }
        ts {
          option 'service=grpc-node,mode=grpc-js'
        }
      }
      task.builtins {
        remove java
        js {
          option 'import_style=commonjs'
        }
      }
    }
  }
}

task syncGeneratedProto(type: Copy) {
  from "$protobuf.generatedFilesBaseDir/main/ts", "$protobuf.generatedFilesBaseDir/main/js"
  into 'proto'
}
