plugins {
  id "com.google.protobuf" version "0.8.12"
  id "java"
}

project.ext.set('protobufVersion', '3.11.0')
project.ext.set('protocVersion', project.protobufVersion)

repositories {
  jcenter()
  mavenCentral()
}

dependencies {}

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')

sourceSets {
  main {
    proto {
      srcDir file('./proto')
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protocVersion}"
  }
  plugins {
    grpc {
      path = file(
        './node_modules/.bin/grpc_tools_node_protoc_plugin' + (isWindows ? '.cmd' : '')
      )
    }
    ts {
      path = file(
        './node_modules/.bin/protoc-gen-ts' + (isWindows ? '.cmd' : '')
      )
    }
  }
  generateProtoTasks {
    generateProto.finalizedBy copyProtoJs, copyProtoTs
    all().each { task ->
      task.plugins {
        grpc {
          outputSubDir = 'js'
        }
        ts {
          option 'service=grpc-node'
        }
      }
      task.builtins {
        remove java
        js {
          option 'import_style=commonjs'
        }
      }
    }
  }
}

task copyProtoJs(type: Copy) {
  dependsOn ':generateProto'
  group 'copy'
  description 'Copies proto JavaScript files into src dir'
  from "$protobuf.generatedFilesBaseDir/main/js"
  into file('out/proto')
}

task copyProtoTs(type: Copy) {
  dependsOn ':generateProto'
  group 'copy'
  description 'Copies proto TypeScript definitions into src dir'
  from "$protobuf.generatedFilesBaseDir/main/ts"
  into file('src/proto')
  filter { line ->
    line.replaceAll('from \"grpc\"', 'from \"@grpc/grpc-js\"')
  }
}
